if(CMAKE_CROSSCOMPILING)

    set(DEVICE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/${EMB_TARGET}")

    if(NOT EXISTS ${DEVICE_DIR})
        message(FATAL_ERROR "Unsupported CubeMX target ${EMB_TARGET}")
    endif()

    # Find the source files generated by CubeMX for this target
    file(GLOB_RECURSE CUBEMX_FILES "${DEVICE_DIR}/**/*.c" "${DEVICE_DIR}/**/*.h")
    file(GLOB STARTUP_FILE "${DEVICE_DIR}/startup_*.s")
    file(GLOB CUBEMX_HAL_DIR "${DEVICE_DIR}/Drivers/*_HAL_Driver")
    file(GLOB CUBEMX_ST_DIR "${DEVICE_DIR}/Drivers/CMSIS/Device/ST/STM*")

    if(NOT CUBEMX_FILES OR NOT EXISTS ${STARTUP_FILE} OR NOT EXISTS ${CUBEMX_HAL_DIR} OR NOT EXISTS ${CUBEMX_ST_DIR})
        message(FATAL_ERROR "Failed to find CubeMX source files")
    endif()

    # Some trickery to get CMake to deal with the assembler code
    set_property(SOURCE ${STARTUP_FILE} PROPERTY LANGUAGE C)

    add_library(cubemx STATIC ${CUBEMX_FILES} ${STARTUP_FILE})

    target_include_directories(cubemx PUBLIC
        ${DEVICE_DIR}/Core/Inc
        ${DEVICE_DIR}/Drivers/CMSIS/Include
        ${CUBEMX_HAL_DIR}/Inc
        ${CUBEMX_HAL_DIR}/Inc/Legacy
        ${CUBEMX_ST_DIR}/Include)

    target_compile_definitions(cubemx PUBLIC USE_HAL_DRIVER)

    if(EMB_TARGET STREQUAL "embedded")
        target_compile_definitions(cubemx PUBLIC STM32F401xE)
    endif()

    # The .ld linker script is already registered inside `dist/*/CMakeLists.txt`

endif()
